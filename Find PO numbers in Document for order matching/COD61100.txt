OBJECT Codeunit 61100 CDC Adv. Find All PO Numbers
{
  OBJECT-PROPERTIES
  {
    Date=07.07.19;
    Time=15:49:25;
    Modified=Yes;
    Version List=CDCADV110.00;
  }
  PROPERTIES
  {
    TableNo=6085590;
    OnRun=BEGIN
            // Process the default Full Capture codeunit first
            CODEUNIT.RUN(CODEUNIT::"Purch. Doc. - Full Capture",Rec);

            FindAllPONumbersInDocument(Rec);
          END;

  }
  CODE
  {

    LOCAL PROCEDURE FindAllPONumbersInDocument@100000008(VAR DCDocument@100000000 : Record 6085590);
    VAR
      DocumentWord@100000002 : Record 6085592;
      PurchaseHeader@100000003 : Record 38;
      PurchaseHeaderBuffer@100000004 : TEMPORARY Record 38;
      TemplateField@100000006 : Record 6085580;
      DocumentValue@100000008 : Record 6085593;
      PurchasesPayablesSetup@100000001 : Record 312;
      NoSeries@100000009 : Record 308;
      NoSeriesLine@100000010 : Record 309;
      CaptureMgt@100000007 : Codeunit 6085576;
      FilterString@100000011 : Text;
      FoundPurchaseOrders@100000005 : Text;
      Pos@100000012 : Integer;
    BEGIN
      DocumentWord.SETRANGE("Document No.",DCDocument."No.");

      // Create appropriate Filter string from Purchase Setup >>>
      PurchasesPayablesSetup.GET;
      NoSeries.GET(PurchasesPayablesSetup."Order Nos.");
      NoSeriesLine.SETRANGE("Series Code",NoSeries.Code);
      NoSeriesLine.SETFILTER("Starting Date",'%1|>=%2',0D,TODAY);
      NoSeriesLine.SETRANGE(Open,TRUE);
      IF NoSeriesLine.FINDFIRST THEN BEGIN
        Pos := 1;

        WHILE (Pos <= STRLEN(NoSeriesLine."Starting No.")) DO BEGIN
          IF NoSeriesLine."Starting No."[Pos] IN ['0'..'9'] THEN
            FilterString += '?'
          ELSE
            FilterString += FORMAT(NoSeriesLine."Starting No."[Pos]);
          Pos += 1;
        END;
      END;

      // Iterate through Document Word table and filter for our PO number filter string
      DocumentWord.SETFILTER(Word,FilterString);
      IF DocumentWord.FINDSET THEN
        REPEAT
          // Check if there is a PO in the system with the matched word
          IF PurchaseHeader.GET(PurchaseHeader."Document Type"::Order,UPPERCASE(DocumentWord.Word)) THEN BEGIN
            // Check if the number exists in the temp. PO buffer
            IF NOT PurchaseHeaderBuffer.GET(PurchaseHeader."Document Type",PurchaseHeader."No.") THEN BEGIN
              PurchaseHeaderBuffer := PurchaseHeader;
              PurchaseHeaderBuffer.INSERT;
            END;
          END;
        UNTIL DocumentWord.NEXT=0;

      // Iterate through all found PO's and create the string, that can be used for order matching
      IF PurchaseHeaderBuffer.FINDFIRST THEN
        REPEAT
          IF (STRLEN(FoundPurchaseOrders) + STRLEN(PurchaseHeaderBuffer."No.") + 1) <= MAXSTRLEN(DocumentValue."Value (Text)") THEN BEGIN
            IF (STRLEN(FoundPurchaseOrders) > 0) THEN
                FoundPurchaseOrders += ',';
              FoundPurchaseOrders += PurchaseHeaderBuffer."No.";
          END;
        UNTIL PurchaseHeaderBuffer.NEXT = 0;

        TemplateField.GET(DCDocument."Template No.",TemplateField.Type::Header,'OURDOCNO');
        CaptureMgt.UpdateFieldValue(DCDocument."No.",0,0,TemplateField,FoundPurchaseOrders,FALSE,FALSE);
    END;

    BEGIN
    END.
  }
}

